macro "Trimming2" {

	diror = "C:/Users/K.Kondo/Desktop/codes/MLtags/images/00.original (bmo)/";	// トリミングする画像を格納しているディレクトリ名指定
	dirbi = "C:/Users/K.Kondo/Desktop/codes/MLtags/images/11.bi_trim/";		// 座標取得のための二値化画像を格納しているディレクトリ名指定
	savedir = "C:/Users/K.Kondo/Desktop/jpg/";				// 編集後の画像格納ディレクトリ指定 
	listor = getFileList(diror);		// diror内の画像データをのリスト取得
	listbi = getFileList(dirbi);		// diror内の画像データをのリスト取得


	for (i=0; i < listbi.length; i++) {
		
		// 変数初期化
		xmin = 2448;	// xmin初期値
		ymin = 2048;	// ymin初期値
		xmax = 0;		// xmax初期値
		ymax = 0;		// ymax初期値
		x0 = 0;		// 切取x座標最小値
		y0 = 0;		// 切取y座標最小値
		dx = 0;		// 切取幅
		dy = 0;		// 切取高さ

		open(dirbi + listbi[i]);
		fname = split(listbi[i], ".");
		sname = fname[0];
		
		for (x = 900; x < 1500; x = x + 50) {
			intensity = getPixel(x, 1400);
			if(intensity == 255) {
				doWand(x, 1400);	// 輪郭を選択
				//run("Measure");	// Measure実行
				getSelectionBounds(BX, BY, width, height);
				if (xmin > BX) { xmin = BX; }
				if (ymin > BY) { ymin = BY; }
				if (xmax < BX + width) { xmax = BX + width; }
				if (ymax < BY + height) { ymax = BY + height; }
			}
		}
	
		x0 = xmin - 10;
		y0 = ymin - 10;
		dx = xmax - xmin + 20;
		dy = ymax - ymin + 20;

		open(diror + listor[i]);
		makeRectangle(x0, y0, dx, dy);

		run("Crop");

		saveAs("png", savedir + sname + ".png");

		close();

	}


}
